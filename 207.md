xiaoming90

high

# Vault accounts with excess cash can avoid being settled

## Summary

If excess cash was transferred out from an account during account settlement, then the protocol will check the account's collateral ratio and revert if the position is unhealthy. Because it may not be possible to settle a vault account, liquidators cannot reduce account debt by purchasing vault shares because `_authenticateDeleverage()` will check to see if a vault has matured.

## Vulnerability Detail

Considering an account's health is determined by a combination of its outstanding debt, cash holdings and the total underlying value of its vault shares, transferring out excess cash may actually put an account in an unhealthy position. 

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultAccountAction.sol#L41-L68

```solidity
File: VaultAccountAction.sol
41:     function settleVaultAccount(address account, address vault) external override nonReentrant {
42:         requireValidAccount(account);
43:         require(account != vault);
44: 
45:         VaultConfig memory vaultConfig = VaultConfiguration.getVaultConfigStateful(vault);
46:         VaultAccount memory vaultAccount = VaultAccountLib.getVaultAccount(account, vaultConfig);
47:         
48:         // Require that the account settled, otherwise we may leave the account in an unintended
49:         // state in this method because we allow it to skip the min borrow check in the next line.
50:         (bool didSettle, bool didTransfer) = vaultAccount.settleVaultAccount(vaultConfig);
51:         require(didSettle, "No Settle");
52: 
53:         vaultAccount.accruePrimeCashFeesToDebt(vaultConfig);
54: 
55:         // Skip Min Borrow Check so that accounts can always be settled
56:         vaultAccount.setVaultAccount({vaultConfig: vaultConfig, checkMinBorrow: false});
57: 
58:         if (didTransfer) {
59:             // If the vault did a transfer (i.e. withdrew cash) we have to check their collateral ratio. There
60:             // is an edge condition where a vault with secondary borrows has an emergency exit. During that process
61:             // an account will be left some cash balance in both currencies. It may have excess cash in one and
62:             // insufficient cash in the other. A withdraw of the excess in one side will cause the vault account to
63:             // be insolvent if we do not run this check. If this scenario indeed does occur, the vault itself must
64:             // be upgraded in order to facilitate orderly exits for all of the accounts since they will be prevented
65:             // from settling.
66:             IVaultAccountHealth(address(this)).checkVaultAccountCollateralRatio(vault, account);
67:         }
68:     }
```

It is important to note that all vault liquidation actions require a vault to first be settled. Hence, through self-liquidation, sophisticated vault accounts can have excess cash in one currency and significant debt holdings in the vault's other currencies. 

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultLiquidationAction.sol#L197-L237

```solidity
File: VaultLiquidationAction.sol
197:     function _authenticateDeleverage(
198:         address account,
199:         address vault,
200:         address liquidator
201:     ) private returns (
202:         VaultConfig memory vaultConfig,
203:         VaultAccount memory vaultAccount,
204:         VaultState memory vaultState
205:     ) {
206:         // Do not allow invalid accounts to liquidate
207:         requireValidAccount(liquidator);
208:         require(liquidator != vault);
209: 
210:         // Cannot liquidate self, if a vault needs to deleverage itself as a whole it has other methods 
211:         // in VaultAction to do so.
212:         require(account != msg.sender);
213:         require(account != liquidator);
214: 
215:         vaultConfig = VaultConfiguration.getVaultConfigStateful(vault);
216:         require(vaultConfig.getFlag(VaultConfiguration.DISABLE_DELEVERAGE) == false);
217: 
218:         // Authorization rules for deleveraging
219:         if (vaultConfig.getFlag(VaultConfiguration.ONLY_VAULT_DELEVERAGE)) {
220:             require(msg.sender == vault);
221:         } else {
222:             require(msg.sender == liquidator);
223:         }
224: 
225:         vaultAccount = VaultAccountLib.getVaultAccount(account, vaultConfig);
226: 
227:         // Vault accounts that are not settled must be settled first by calling settleVaultAccount
228:         // before liquidation. settleVaultAccount is not permissioned so anyone may settle the account.
229:         require(block.timestamp < vaultAccount.maturity, "Must Settle");
230: 
231:         if (vaultAccount.maturity == Constants.PRIME_CASH_VAULT_MATURITY) {
232:             // Returns the updated prime vault state
233:             vaultState = vaultAccount.accruePrimeCashFeesToDebtInLiquidation(vaultConfig);
234:         } else {
235:             vaultState = VaultStateLib.getVaultState(vaultConfig, vaultAccount.maturity);
236:         }
237:     }
```

Consider the following example:

Alice has a valid borrow position in the vault which is considered risky. She has a small bit of secondary cash but most of her debt is primary currency denominated. Generally speaking her vault is healthy. Upon settlement, the small bit of excess secondary cash is transferred out and her vault is undercollateralised and eligible for liquidation. However, we are deadlocked because it is not possible to settle the vault because `checkVaultAccountCollateralRatio()` will fail, and it's not possible to purchase the excess cash and offset the debt directly via `liquidateVaultCashBalance()` or `deleverageAccount()` because `_authenticateDeleverage()` will revert if a vault has not yet been settled.

## Impact

Vault accounts can create positions which will never be eligible for liquidation and the protocol may accrue bad debt.

## Code Snippet

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultAccountAction.sol#L41-L68

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/external/actions/VaultLiquidationAction.sol#L197-L237

## Tool used

Manual Review

## Recommendation

Consider adding a liquidation method which settles a vault account and allows for a liquidator to purchase vault shares, offsetting outstanding debt, before performing collateral ratio checks.