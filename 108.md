ShadowForce

high

# Token approval is never issued for CERC20 When executing rebalancing in TreasuryAction.sol

## Summary
Token approval is never issued
## Vulnerability Detail
```solidity
    function rebalance(uint16[] calldata currencyId) external override onlyManagerContract {
        for (uint256 i; i < currencyId.length; ++i) {
            _rebalanceCurrency(currencyId[i]);
        }
    }
```
in the snippet above we can observe the `rebalance` function calls into the function `_rebalanceCurrency`
`_rebalanceCurrency` then calls into the function below

```solidity
    function _executeRebalance(uint16 currencyId) private {
        IPrimeCashHoldingsOracle oracle = PrimeCashExchangeRate.getPrimeCashHoldingsOracle(currencyId);
        uint8[] memory rebalancingTargets = _getRebalancingTargets(currencyId, oracle.holdings());
        (RebalancingData memory data) = REBALANCING_STRATEGY.calculateRebalance(oracle, rebalancingTargets);

        (/* */, uint256 totalUnderlyingValueBefore) = oracle.getTotalUnderlyingValueStateful();

        // Process redemptions first
        Token memory underlyingToken = TokenHandler.getUnderlyingToken(currencyId);
        TokenHandler.executeMoneyMarketRedemptions(underlyingToken, data.redeemData);

        // Process deposits
        _executeDeposits(underlyingToken, data.depositData);
```
the function above then calls into
```solidity
   function _executeDeposits(Token memory underlyingToken, DepositData[] memory deposits) private {
        uint256 totalUnderlyingDepositAmount;
        
        for (uint256 i; i < deposits.length; i++) {
            DepositData memory depositData = deposits[i];
            // Measure the token balance change if the `assetToken` value is set in the
            // current deposit data struct. 
            uint256 oldAssetBalance = IERC20(depositData.assetToken).balanceOf(address(this));

            // Measure the underlying balance change before and after the call.
            uint256 oldUnderlyingBalance = underlyingToken.balanceOf(address(this));

            for (uint256 j; j < depositData.targets.length; ++j) {
                // This will revert if the individual call reverts.
                GenericToken.executeLowLevelCall(
                    depositData.targets[j], 
                    depositData.msgValue[j], 
                    depositData.callData[j]
                );
            }
```
let us back track a bit and take a look at the `_executeRebalance` function again
in this function the snippet below is present.
```solidity
(RebalancingData memory data) = REBALANCING_STRATEGY.calculateRebalance(oracle, rebalancingTargets);
```
this is calling
```solidity
        rebalancingData.redeemData = oracle.getRedemptionCalldataForRebalancing(redeemHoldings, redeemAmounts);
        rebalancingData.depositData = oracle.getDepositCalldataForRebalancing(depositHoldings, depositAmounts);
```
oracle.getDepositCalldataForRebalancing is important
calling
```solidity
    function getDepositCalldataForRebalancing(
        address[] calldata holdings_, 
        uint256[] calldata depositAmounts
    ) external view override returns (DepositData[] memory depositData) {
        return _getDepositCalldataForRebalancing(holdings_, depositAmounts);
    }

```
which calls
```solidity
    function _getDepositCalldataForRebalancing(
        address[] calldata holdings, 
        uint256[] calldata depositAmounts
    ) internal view virtual override returns (
        DepositData[] memory depositData
    ) {
        require(holdings.length == NUM_ASSET_TOKENS && holdings[0] == COMPOUND_ASSET_TOKEN);
        return CompoundV2AssetAdapter.getDepositCalldata({
            from: address(NOTIONAL),
            assetToken: COMPOUND_ASSET_TOKEN,
            assetRateAdapter: COMPOUND_RATE_ADAPTER,
            rateAdapterPrecision: RATE_ADAPTER_PRECISION,
            depositUnderlyingAmount: depositAmounts[0],
            underlyingIsETH: UNDERLYING_IS_ETH
        });
    }
```
finally this then calls CompoundV2AssetAdapter.getDepositCalldata
```solidity
    function getDepositCalldata(
        address from,
        address assetToken,
        address assetRateAdapter,
        uint256 rateAdapterPrecision,
        uint256 depositUnderlyingAmount,
        bool underlyingIsETH
    ) internal view returns (DepositData[] memory data) {
        address[] memory targets = new address[](1);
        bytes[] memory callData = new bytes[](1);
        uint256[] memory msgValue = new uint256[](1);

        targets[0] = assetToken;
        msgValue[0] = underlyingIsETH ? depositUnderlyingAmount : 0;
        callData[0] = abi.encodeWithSelector(
            underlyingIsETH ? CEtherInterface.mint.selector : CErc20Interface.mint.selector, 
            depositUnderlyingAmount
        );

        data = new DepositData[](1);
        data[0] = DepositData(targets, callData, msgValue, depositUnderlyingAmount, assetToken);
    }
```
in the functions logic above we can see that the token is cEther or it is cERC20. the problem here is that the logic never generates call data for token approval which is needed for cERC20 tokens. Without the approval, the tokens can not be transfered. Therefore rebalance cannot happen successfully which ultimately can lead to bad debt, and loss of potential funds for the protocol and its users.
## Impact
Without generation of token approval call data for CERC20 tokens. CERC20 tokens cannot be transferred. Therefore rebalance does not function properly. This could lead to bad debt and loss of funds for the protocol and its users.
## Code Snippet
https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/actions/TreasuryAction.sol#L224-L263

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/actions/TreasuryAction.sol#L284-L302

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/actions/TreasuryAction.sol#L304-L323

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/pCash/UnderlyingHoldingsOracle.sol#L80-L85

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/pCash/CompoundV2HoldingsOracle.sol#L87-L103

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/pCash/adapters/CompoundV2AssetAdapter.sol#L31-L52
## Tool used

Manual Review

## Recommendation
Recommend to add logic that generates token approval for CERC20 tokens.