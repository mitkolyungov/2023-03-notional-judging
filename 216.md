iglyx

high

# TokenHandler's _redeemMoneyMarketIfRequired can fail valid withdrawal requests

## Summary

_redeemMoneyMarketIfRequired() tries to redeem extra funds from Compound, which leads to fund freeze on every lending market liquidity shortage. I.e. in the situation when the funds needed are available, there will be withdrawal denial just because more funds than needed are requested, and such a call denied by lending pool.

## Vulnerability Detail

Compound either withdraws the whole `withdrawAmountExternal` or revert, so checking `withdrawAmountExternal <= currentBalance.add(totalUnderlyingRedeemed)` in _redeemMoneyMarketIfRequired() doesn't really control the amount, and, more importantly, all the cases when there is `withdrawAmountExternal - currentBalance` available to be withdrawn, but there is no `withdrawAmountExternal` available, will be reverted, which constitutes a substantial denial of service, say when current balance is big enough and there is not much funds invested.

Other lending markets tends to behave similarly, say Aave will also revert the call for too big withdrawal, both when there is not enough funds for the Notional user or when it's not enough free liquidity to perform the withdrawal.

## Impact

User funds will be frozen for an arbitrary time whenever `withdrawAmountExternal - currentBalance` is much smaller than `withdrawAmountExternal` and the latter is not available from the lending pool at the moment.

## Code Snippet

Instead of trying to redeem `withdrawAmountExternal - currentBalance` _redeemMoneyMarketIfRequired() calls for the whole `withdrawAmountExternal`:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/balances/TokenHandler.sol#L249-L278

```solidity
    /// @notice Prime cash holdings may be in underlying tokens or they may be held in other money market
    /// protocols like Compound, Aave or Euler. If there is insufficient underlying tokens to withdraw on
    /// the contract, this method will redeem money market tokens in order to gain sufficient underlying
    /// to withdraw from the contract.
    /// @param currencyId associated currency id
    /// @param underlying underlying token information
    /// @param withdrawAmountExternal amount of underlying to withdraw in external token precision
    function _redeemMoneyMarketIfRequired(
        uint16 currencyId,
        Token memory underlying,
        uint256 withdrawAmountExternal
    ) private {
        // If there is sufficient balance of the underlying to withdraw from the contract
        // immediately, just return.
        mapping(address => uint256) storage store = LibStorage.getStoredTokenBalances();
        uint256 currentBalance = store[underlying.tokenAddress];
        if (withdrawAmountExternal <= currentBalance) return;

        IPrimeCashHoldingsOracle oracle = PrimeCashExchangeRate.getPrimeCashHoldingsOracle(currencyId);
        // Redemption data returns an array of contract calls to make from the Notional proxy (which
        // is holding all of the money market tokens).
@>      (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal);

        // This is the total expected underlying that we should redeem after all redemption calls
        // are executed.
        uint256 totalUnderlyingRedeemed = executeMoneyMarketRedemptions(underlying, data);

        // Ensure that we have sufficient funds before we exit
@>      require(withdrawAmountExternal <= currentBalance.add(totalUnderlyingRedeemed)); // dev: insufficient redeem
    }
```

`withdrawAmount` is then passed to Compound:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/pCash/UnderlyingHoldingsOracle.sol#L69-L71

```solidity
    function getRedemptionCalldata(uint256 withdrawAmount) external view override returns (RedeemData[] memory redeemData) {
@>      return _getRedemptionCalldata(withdrawAmount);
    }
```

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/pCash/CompoundV2HoldingsOracle.sol#L64-L75

```solidity
    /// @notice Returns calldata for how to withdraw an amount
    function _getRedemptionCalldata(uint256 withdrawAmount) internal view virtual override returns (
        RedeemData[] memory redeemData
    ) {
        return CompoundV2AssetAdapter.getRedemptionCalldata({
            from: address(NOTIONAL),
            assetToken: COMPOUND_ASSET_TOKEN,
            assetRateAdapter: COMPOUND_RATE_ADAPTER,
            rateAdapterPrecision: RATE_ADAPTER_PRECISION,
@>          redeemUnderlyingAmount: withdrawAmount
        });
    }
```

But Compound will revert the call if current liquidity do not allow for the whole `redeemUnderlyingAmount` to be withdrawn:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/external/pCash/adapters/CompoundV2AssetAdapter.sol#L10-L25

```solidity
library CompoundV2AssetAdapter {
    function getRedemptionCalldata(
        address from,
        address assetToken,
        address assetRateAdapter,
        uint256 rateAdapterPrecision,
        uint256 redeemUnderlyingAmount
    ) internal view returns (RedeemData[] memory data) {
        address[] memory targets = new address[](1);
        bytes[] memory callData = new bytes[](1);
        targets[0] = assetToken;
@>      callData[0] = abi.encodeWithSelector(CErc20Interface.redeemUnderlying.selector, redeemUnderlyingAmount);

        data = new RedeemData[](1);
@>      data[0] = RedeemData(targets, callData, redeemUnderlyingAmount, assetToken);
    }
```

This way the require (`totalUnderlyingRedeemed >= withdrawAmountExternal - currentBalance`) will not be effective control as Compound will enforce more strict condition of being able to redeem the whole `withdrawAmountExternal`:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/balances/TokenHandler.sol#L249-L278

```solidity
    function _redeemMoneyMarketIfRequired(
        uint16 currencyId,
        Token memory underlying,
        uint256 withdrawAmountExternal
    ) private {
        ...
        (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal);

        // This is the total expected underlying that we should redeem after all redemption calls
        // are executed.
        uint256 totalUnderlyingRedeemed = executeMoneyMarketRedemptions(underlying, data);

        // Ensure that we have sufficient funds before we exit
@>      require(withdrawAmountExternal <= currentBalance.add(totalUnderlyingRedeemed)); // dev: insufficient redeem
    }
```

## Tool used

Manual Review

## Recommendation

Consider requesting only the part that's needed:

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/balances/TokenHandler.sol#L249-L278

```diff
    /// @notice Prime cash holdings may be in underlying tokens or they may be held in other money market
    /// protocols like Compound, Aave or Euler. If there is insufficient underlying tokens to withdraw on
    /// the contract, this method will redeem money market tokens in order to gain sufficient underlying
    /// to withdraw from the contract.
    /// @param currencyId associated currency id
    /// @param underlying underlying token information
    /// @param withdrawAmountExternal amount of underlying to withdraw in external token precision
    function _redeemMoneyMarketIfRequired(
        uint16 currencyId,
        Token memory underlying,
        uint256 withdrawAmountExternal
    ) private {
        // If there is sufficient balance of the underlying to withdraw from the contract
        // immediately, just return.
        mapping(address => uint256) storage store = LibStorage.getStoredTokenBalances();
        uint256 currentBalance = store[underlying.tokenAddress];
        if (withdrawAmountExternal <= currentBalance) return;

        IPrimeCashHoldingsOracle oracle = PrimeCashExchangeRate.getPrimeCashHoldingsOracle(currencyId);
        // Redemption data returns an array of contract calls to make from the Notional proxy (which
        // is holding all of the money market tokens).
-       (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal);
+       (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal - currentBalance);

        // This is the total expected underlying that we should redeem after all redemption calls
        // are executed.
        uint256 totalUnderlyingRedeemed = executeMoneyMarketRedemptions(underlying, data);

        // Ensure that we have sufficient funds before we exit
        require(withdrawAmountExternal <= currentBalance.add(totalUnderlyingRedeemed)); // dev: insufficient redeem
    }
```