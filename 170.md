bin2chen

medium

# _redeemMoneyMarketIfRequired()  redeem too much money from market

## Summary
in `_redeemMoneyMarketIfRequired()` 
Wrong amount of money specified for redemption from the market (use `withdrawAmountExternal`)
It makes more sense to use `withdrawAmountExternal-currentBalance`
Avoid too many redemptions and revert

## Vulnerability Detail

`_redeemMoneyMarketIfRequired()` If the current balance is insufficient, an attempt will be made to redeem the amount from the market
The code is as follows:

```solidity
    function _redeemMoneyMarketIfRequired(
        uint16 currencyId,
        Token memory underlying,
        uint256 withdrawAmountExternal
    ) private {
        // If there is sufficient balance of the underlying to withdraw from the contract
        // immediately, just return.
        mapping(address => uint256) storage store = LibStorage.getStoredTokenBalances();
        uint256 currentBalance = store[underlying.tokenAddress];
        if (withdrawAmountExternal <= currentBalance) return;

        IPrimeCashHoldingsOracle oracle = PrimeCashExchangeRate.getPrimeCashHoldingsOracle(currencyId);
        // Redemption data returns an array of contract calls to make from the Notional proxy (which
        // is holding all of the money market tokens).
        (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal);  //<---------@audit need use withdrawAmountExternal-currentBalance

        // This is the total expected underlying that we should redeem after all redemption calls
        // are executed.
        uint256 totalUnderlyingRedeemed = executeMoneyMarketRedemptions(underlying, data);

        // Ensure that we have sufficient funds before we exit
        require(withdrawAmountExternal <= currentBalance.add(totalUnderlyingRedeemed)); // dev: insufficient redeem
    }
```

As we can see from the above code, if the current balance is not enough, redemptions will be made
But the amount of redemption is done using `withdrawAmountExternal` instead of the insufficient amount (withdrawAmountExternal - currentBalance)

This may lead to failure by expecting too much money to be redeemed

For example.
currentBalance = 500 , money market total = 500

1.alice need withdrawAmountExternal = 600
2. in `_redeemMoneyMarketIfRequired()` `currentBalance` is not enough, resulting in the execution of `executeMoneyMarketRedemptions(600)`
3. Since the market amount is only 500, `executeMoneyMarketRedemptions` will revert
```solidity
    function executeMoneyMarketRedemptions(
...
            require(data.expectedUnderlying <= underlyingBalanceChange);
```

But in reality, you only need to get back 600-500=100, so you should not revert


## Impact

funds are sufficient but can't execute transcation 

## Code Snippet

https://github.com/sherlock-audit/2023-03-notional/blob/main/contracts-v2/contracts/internal/balances/TokenHandler.sol#L270

## Tool used

Manual Review

## Recommendation

```solidity
    function _redeemMoneyMarketIfRequired(
        uint16 currencyId,
        Token memory underlying,
        uint256 withdrawAmountExternal
    ) private {
...

-       (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal); 
+       (RedeemData[] memory data) = oracle.getRedemptionCalldata(withdrawAmountExternal - currentBalance); 
```