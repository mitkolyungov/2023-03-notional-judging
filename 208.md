xiaoming90

high

# `_authenticateDeleverage()` will revert if cash holdings exceed vault fees

## Summary

If a prime vault is being liquidated, there is an edge case where `accruePrimeCashFeesToDebt()` will revert if `vaultAccount.tempCashBalance` is positive.

## Vulnerability Detail

`_authenticateDeleverage()` is called as a precursor to any vault liquidation action. If the vault being liquidated is a prime vault, there is an edge case whereby the function will revert and therefore the vault will not be liquidatable.

The function first calls `getVaultAccount()` which populates `vaultAccount.tempCashBalance` with some positive amount. 

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/internal/vaults/VaultAccount.sol#L54-L70

```solidity
File: VaultAccount.sol
54:     function getVaultAccount(
55:         address account, VaultConfig memory vaultConfig
56:     ) internal view returns (VaultAccount memory vaultAccount) {
57:         mapping(address => mapping(address => VaultAccountStorage)) storage store = LibStorage.getVaultAccount();
58:         VaultAccountStorage storage s = store[account][vaultConfig.vault];
59: 
60:         vaultAccount.maturity = s.maturity;
61:         vaultAccount.vaultShares = s.vaultShares;
62:         vaultAccount.account = account;
63:         // Read any temporary cash balance onto the stack to be applied
64:         vaultAccount.tempCashBalance = int256(uint256(s.primaryCash));
65:         vaultAccount.lastUpdateBlockTime = s.lastUpdateBlockTime;
66: 
67:         vaultAccount.accountDebtUnderlying = VaultStateLib.readDebtStorageToUnderlying(
68:             vaultConfig.primeRate, vaultAccount.maturity, s.accountDebt
69:         );
70:     }
```

Vault fees are assessed, deducting some amount `>= 0` from `vaultAccount.tempCashBalance`. It is more than likely that `vaultAccount.tempCashBalance` would still be some positive amount and hence the function will revert in `accruePrimeCashFeesToDebt()` because it expects excess positive cash to be sent back to the account during `settleAccountfCashBalance()`. But this function is never called because the prime vault has already been settled prior to liquidation.

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/internal/vaults/VaultAccount.sol#L508-L546

```solidity
File: VaultAccount.sol
508:     function accruePrimeCashFeesToDebtInLiquidation(
509:         VaultAccount memory vaultAccount,
510:         VaultConfig memory vaultConfig
511:     ) internal returns (VaultState memory) {
512:         vaultConfig.assessVaultFees(
513:             vaultAccount,
514:             vaultConfig.primeRate.convertFromUnderlying(vaultAccount.accountDebtUnderlying).neg(),
515:             vaultAccount.maturity,
516:             block.timestamp
517:         );
518: 
519:         return accruePrimeCashFeesToDebt(vaultAccount, vaultConfig);
520:     }
521: 
522:     /// @notice Accrues prime cash fees directly to debt during settlement and liquidation
523:     function accruePrimeCashFeesToDebt(
524:         VaultAccount memory vaultAccount,
525:         VaultConfig memory vaultConfig
526:     ) internal returns (VaultState memory vaultPrimeState) {
527:         require(vaultAccount.maturity == Constants.PRIME_CASH_VAULT_MATURITY);
528: 
529:         // During settle vault account, the prime cash fee is accrued to debt instead
530:         // of left in the tempCashBalance.
531:         vaultPrimeState = VaultStateLib.getVaultState(vaultConfig, Constants.PRIME_CASH_VAULT_MATURITY);
532: 
533:         // Fees and prime cash claims will be held in temp cash balance. There cannot be a positive cash balance
534:         // during this method, any excess positive cash should be sent back to the account during
535:         // settleAccountfCashBalance
536:         require(vaultAccount.tempCashBalance <= 0);
537:         
538:         updateAccountDebt(
539:             vaultAccount,
540:             vaultPrimeState,
541:             vaultConfig.primeRate.convertToUnderlying(vaultAccount.tempCashBalance),
542:             vaultAccount.tempCashBalance.neg()
543:         );
544: 
545:         vaultPrimeState.setVaultState(vaultConfig);
546:     }
```

## Impact

It may not be possible to liquidate certain vault accounts, leading to bad debt accrual.

## Code Snippet

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/internal/vaults/VaultAccount.sol#L54-L70

https://github.com/sherlock-audit/2023-03-notional-0xleastwood/blob/main/contracts-v2/contracts/internal/vaults/VaultAccount.sol#L508-L546

## Tool used

Manual Review

## Recommendation

Consider updating this `require` statement to an `if` statement or remove it altogether.