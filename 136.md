ShadowForce

high

# Bypass the VAULT_ACCOUNT_MIN_TIME check in VaultAccountAction.sol when exiting the vault allows potential MEV

## Summary

Bypass the VAULT_ACCOUNT_MIN_TIME check in VaultAccountAction.sol when exiting the vault allows potential MEV

## Vulnerability Detail

In VaultAccountAction.sol#exitVault, we have this function

```solidity
    /// @notice Allows an account to withdraw their position from the vault at any time. Will
    /// redeem some number of vault shares to the borrow currency and close the borrow position by
    /// lending. Any shortfall in cash from lending will be transferred from the account, any excess
    /// profits will be transferred to the account.
    /// @param account the address that will exit the vault
    /// @param vault the vault to enter
    /// @param receiver the address that will receive profits
    /// @param vaultSharesToRedeem amount of vault tokens to exit, only relevant when exiting pre-maturity
    /// @param lendAmount amount of fCash to lend if fixed, amount of underlying to lend if in pCash
    /// @param minLendRate the minimum rate to lend at
    /// @param exitVaultData passed to the vault during exit
    /// @return underlyingToReceiver amount of underlying tokens returned to the receiver on exit
    function exitVault(
        address account,
        address vault,
        address receiver,
        uint256 vaultSharesToRedeem,
        uint256 lendAmount,
        uint32 minLendRate,
        bytes calldata exitVaultData
    ) external payable override nonReentrant returns (uint256 underlyingToReceiver) {
        VaultConfig memory vaultConfig = VaultConfiguration.getVaultConfigStateful(vault);
        vaultConfig.authorizeCaller(account, VaultConfiguration.ONLY_VAULT_EXIT);

        // If the vault allows further re-entrancy then set the status back to the default
        if (vaultConfig.getFlag(VaultConfiguration.ALLOW_REENTRANCY)) {
            reentrancyStatus = _NOT_ENTERED;
        }

        VaultAccount memory vaultAccount = VaultAccountLib.getVaultAccount(account, vaultConfig);
        require(vaultAccount.lastUpdateBlockTime + Constants.VAULT_ACCOUNT_MIN_TIME <= block.timestamp);

```

note this check

```solidity
        VaultAccount memory vaultAccount = VaultAccountLib.getVaultAccount(account, vaultConfig);
        require(vaultAccount.lastUpdateBlockTime + Constants.VAULT_ACCOUNT_MIN_TIME <= block.timestamp);
```

what does this check mean?

```solidity
    // Requires vault accounts to enter a position for a minimum of 1 min
    // to mitigate strange behavior where accounts may enter and exit using
    // flash loans or other MEV type behavior.
    uint256 internal constant VAULT_ACCOUNT_MIN_TIME = 1 minutes;
```

as we can see, basically this check means that the protocol clearly want to use a cooldown period to mitigate flashloan behaivor such as enter or exit the vault at the same block, clearly sponsor want to mitigate strange behavior where accounts may enter and exit using and flash loans or other MEV type behavior.

this check is implemented in VaultAccountAction.sol#exitVault.sol

however, this is checking is missing in the function 

```solidity
    /// @notice Re-enters the vault at a different maturity. The account's existing borrow position will be closed
    /// and a new borrow position at the specified maturity will be opened. Strategy token holdings will transfer
    /// to the new maturity. Accounts can roll to longer or shorter dated maturities.
    /// @param account the address that will reenter the vault
    /// @param vault the vault to reenter
    /// @param newBorrowAmount amount of fCash to borrow in the next maturity
    /// @param maturity new maturity to borrow at
    /// @param depositAmountExternal amount to deposit into the new maturity
    /// @param minLendRate slippage protection for repaying debts
    /// @param maxBorrowRate slippage protection for new borrow position
    /// @return vaultSharesAdded the total strategy tokens added to the maturity, including any tokens
    /// rolled from the previous maturity. Allows rollVaultPosition to be used by off-chain methods to get
    /// an accurate simulation of the strategy tokens minted.
    function rollVaultPosition(
        address account,
        address vault,
        uint256 newBorrowAmount,
        uint256 maturity,
        uint256 depositAmountExternal,
        uint32 minLendRate,
        uint32 maxBorrowRate,
        bytes calldata enterVaultData
    ) external payable override nonReentrant returns (uint256 vaultSharesAdded) {
```

this function calls

```solidity
// Exit the vault first and debit the temporary cash balance with the cost to exit
vaultAccount.lendToExitVault(
	vaultConfig,
	vaultState,
	vaultAccount.accountDebtUnderlying.neg(), // must fully exit the fCash position
	minLendRate,
	block.timestamp
);
```

as well!

basically user can use rollVaultPosition to bypass the VAULT_ACCOUNT_MIN_TIME check in VaultAccountAction.sol#exitVault.sol

## Additional context

when enter the vault, we are calling

```solidity
VaultAccount memory vaultAccount = VaultAccountLib.getVaultAccount(account, vaultConfig);
vaultAccount.settleAccountOrAccruePrimeCashFees(vaultConfig);
```

this is calling settleAccountOrAccruePrimeCashFees, which update the state vaultAccount.lastUpdateBlockTime

```solidity

    function settleAccountOrAccruePrimeCashFees(
        VaultAccount memory vaultAccount,
        VaultConfig memory vaultConfig
    ) internal returns (bool didSettle) {
        // If the vault has matured, it will exit this settlement call in the prime cash maturity with
        // fees assessed up to the current time. Transfers may occur but they are not relevant in this
        // context since a collateral check will always be done on non-settlement methods.
        (didSettle, /* */) = settleVaultAccount(vaultAccount, vaultConfig);

        // If the account did not settle but is in the prime cash maturity, assess a fee.
        if (!didSettle && vaultAccount.maturity == Constants.PRIME_CASH_VAULT_MATURITY) {
            // The prime cash fee is deducted from the tempCashBalance
            vaultConfig.assessVaultFees(
                vaultAccount,
                vaultConfig.primeRate.convertFromUnderlying(vaultAccount.accountDebtUnderlying).neg(),
                vaultAccount.maturity,
                block.timestamp
            );
        }
    }
```

this is calling assessVaultFees

```solidity
    function assessVaultFees(
        VaultConfig memory vaultConfig,
        VaultAccount memory vaultAccount,
        int256 primeCashBorrowed,
        uint256 maturity,
        uint256 blockTime
    ) internal returns (int256 netTotalFee) {
        netTotalFee = calculateVaultFees(vaultConfig, vaultAccount, primeCashBorrowed, maturity, blockTime);
```

this is calling calculateVaultFees

```solidity
    function calculateVaultFees(
        VaultConfig memory vaultConfig,
        VaultAccount memory vaultAccount,
        int256 primeCashBorrowed,
        uint256 maturity,
        uint256 blockTime
    ) internal pure returns (int256 netTotalFee) {
        int256 proratedFeeTime;
        if (maturity == Constants.PRIME_CASH_VAULT_MATURITY) {
            // vaultAccount.maturity is set after assessVaultFees on the initial entry
            // to the prime cash maturity, so if it is not set here then we do not
            // assess a fee on the vault account. The fee for its initial period will
            // be assessed on the next time it borrows more or exits the vaults.
            if (vaultAccount.maturity != Constants.PRIME_CASH_VAULT_MATURITY) return 0;

            // Prime cash vaults do not have a maturity, so accounts are assessed
            // a fee based on how long they have borrowed from the vault.
            // proratedFeeTime = (blockTime - lastUpdateBlockTime)
            // NOTE: this means fees must be assessed on exit and entry
            proratedFeeTime = blockTime.sub(vaultAccount.lastUpdateBlockTime).toInt();
            // Set the timer here so that we do not double assess fees later
            vaultAccount.lastUpdateBlockTime = blockTime;
        } else {
            proratedFeeTime = maturity.sub(blockTime).toInt();
        }

        // The fee rate is annualized, we prorate it linearly based on the time to maturity here
        int256 proratedFeeRate = vaultConfig.feeRate
            .mul(proratedFeeTime)
            .div(int256(Constants.YEAR));

        netTotalFee = primeCashBorrowed.mulInRatePrecision(proratedFeeRate);
    }
```

and in the line of code

```solidity
vaultAccount.lastUpdateBlockTime = blockTime;
```

this is how vaultAccount.lastUpdateBlockTime is set

## Impact

Bypass the VAULT_ACCOUNT_MIN_TIME check in VaultAccountAction.sol when exiting the vault allows potential MEV

## Code Snippet

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/actions/VaultAccountAction.sol#L242

https://github.com/notional-finance/contracts-v2/blob/b20a45c912785fab5f2b62992e5260f44dbae197/contracts/external/actions/VaultAccountAction.sol#L179

## Tool used

Manual Review

## Recommendation

We recommend the protocol add the check of VAULT_ACCOUNT_MIN_TIME in rollVaultPosition as well!
